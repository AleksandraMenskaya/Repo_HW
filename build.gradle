plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.11.2'
}

repositories {
    mavenCentral()
}

def allureVersion = "2.21.0",
    selenideVersion = "6.19.1",
    junitVersion = "5.10.0",
    slf4jVersion = "2.0.7",
    ownerVersion = "1.0.4"

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

dependencies {
    testImplementation(
            "com.codeborne:selenide:$selenideVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "org.slf4j:slf4j-simple:$slf4jVersion",
            'com.github.javafaker:javafaker:1.0.2',
            "org.aeonbits.owner:owner:$ownerVersion"
    )
}

tasks.withType(Test) {
    useJUnitPlatform()

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task formFill_Test(type: Test) {
    useJUnitPlatform {
        includeTags("formFill_Test")
    }
}

task positive_tests(type: Test) {
    useJUnitPlatform {
        includeTags("positive")
    }
}

task negative_tests(type: Test) {
    useJUnitPlatform {
        includeTags("negative")
    }
}